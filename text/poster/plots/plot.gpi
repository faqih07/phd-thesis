#======================================================================
foutput(n) = sprintf("plots/%s.tex",n)
fresult(n) = sprintf("../../../results/%s.dat",n)
faux(n) = sprintf("../../main/plots/aux/%s.dat",n)

mili(v) = (1e3*v)
kilo(v) = (1e-3*v)
mega(v) = (1e-6*v)

myfont = ",28"

W = 2.5*6.0
H = 2.5*3.0

#======================================================================
reset
set term cairolatex pdf input size .5*W,.8*H font myfont
load "settings.gpi"

set log x
set xlabel "$\\linkMaterialStiffnessShear/\\linkMaterialStiffnessNormal$"
b = "stiffnessMatrix"
bbo = b."_oofem_macromicro"
bby = b."_yade_macromicro"
set xr [1e-3:1e4]
young = 25e9
nn = 11
pps = 3
pt1 = 1
pt2 = 4
pt3 = 2
lww = 40

set xtics format "$\\scriptstyle{10^{%T}}$"
set xtics 10
unset mxtics
sumladiv3v = 2.61086893561
ir = "1.50"
#
set yr [0:*]
set ytics auto
unset mytics
set ylabel "$\\youngModulus_\\avg/\\linkMaterialStiffnessNormal$"
set output foutput(sprintf("preprocessed_macromicro_E_I%s",ir))
set key left top
plot \
	sumladiv3v*(2+3*x)/(4+x) w l lt 4 lw lww t "eq. (2)" , \
	fresult(sprintf("preprocessed_%s_I%s_theor",bby,ir)) every ::0::nn u 1:($2/young) w p lt 1 pt pt1 ps pps t "eq. (1)" , \
	fresult(sprintf("preprocessed_%s_I%s",bby,ir)) every ::0::nn u 1:($2/young) w p lt 1 pt pt2 ps pps t "DEM" , \
	fresult(sprintf("preprocessed_%s_I%s",bbo,ir)) every ::0::nn u 1:($2/young) w p lt 1 pt pt3 ps pps t "FEM"
#
set yr [-1:.5]
set ytics .5
set mytics 5
set grid mytics
set key right top
set ylabel "$\\poissonRatio_\\avg$"
set output foutput(sprintf("preprocessed_macromicro_nu_I%s",ir))
plot \
	(1-x)/(4+x) w l lt 4 lw lww t "eq. (2)" , \
	fresult(sprintf("preprocessed_%s_I%s_theor",bby,ir)) u 1:3 w p lt 1 pt pt1 ps pps t "eq. (1)" , \
	fresult(sprintf("preprocessed_%s_I%s",bby,ir)) u 1:3 w p lt 1 pt pt2 ps pps t "DEM" , \
	fresult(sprintf("preprocessed_%s_I%s",bbo,ir)) u 1:3 w p lt 1 pt pt3 ps pps t "FEM"

#======================================================================
reset
set term cairolatex pdf input size W,.8*H font myfont
load "settings.gpi"
set yr [.8:1.5]
set xr [-.5:2.5]
set xtics ("9.5" 0, "12.5" 1, "19.0" 2) rotate by 90 right
b = "validationBeygi"
pb = "preprocessed_".b
set pointsize 1
titles = "$\\youngModulus$ $f_t$ $f_c$ $G_f$ {}$"
experiments = "h l h l"
set output foutput(b)
set multiplot layout 1,4 margins screen .10, .95, 0.30, .90 spacing screen 0.08, 0.1
unset key
lww = 30
lww2 = 10
set pointsize 2
do for [i=1:4] {
	set title word(titles,i)
	experiment = word(experiments,i)
	plot \
		faux(b."_experiment_".experiment)      u 5+i w lp lt 4 pt 1 lw lww t "experiment" , \
		fresult(pb."_(.5,.5,.1)_".experiment)  u 5+i w lp lt 2 lw lww2 t "simulation 1" , \
		fresult(pb."_(.5,.5,.05)_".experiment) u 5+i w lp lt 3 lw lww2 t "simulation 2" , \
		NaN t ""
	unset key
}
unset multiplot
set term cairolatex pdf input size .33*W,.28*H font myfont
set yr [20:21]
unset border
unset tics
unset title
set key box vertical
set output foutput(b."_legend")
plot \
	faux(b."_experiment_".experiment)      u 5+i w lp lt 4 pt 1 lw lww t "experiment" , \
	fresult(pb."_(.5,.5,.1)_".experiment)  u 5+i w lp lt 2 t "simulation 1" , \
	fresult(pb."_(.5,.5,.05)_".experiment) u 5+i w lp lt 3 t "simulation 2" , \
	NaN t ""
