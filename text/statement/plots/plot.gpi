#======================================================================
foutput(n) = sprintf("plots/%s.tex",n)
fresult(n) = sprintf("../../../results/%s.dat",n)
faux(n) = sprintf("aux/%s.dat",n)

mili(v) = (1e3*v)
kilo(v) = (1e-3*v)
mega(v) = (1e-6*v)

W = 6.0
H = 3.0


#======================================================================
reset
set term cairolatex pdf input size .5*W,H
load "settings.gpi"

set log x
set xlabel "$\\linkMaterialStiffnessShear/\\linkMaterialStiffnessNormal$"
b = "stiffnessMatrix"
bbo = b."_oofem_macromicro"
bby = b."_yade_macromicro"
set xr [1e-3:1e4]
young = 25e9
sumladiv3vs = "1.22730276283 1.84128661899 2.61086893561 10.4474880903"
irs = "1.05 1.25 1.50 2.00"
nn = 11
pps = 1.2
pt1 = 1
pt2 = 4
pt3 = 2

set xtics format "$\\scriptstyle{10^{%T}}$"
set xtics 10
unset mxtics
set key width -46
do for [i=1:4] {
	sumladiv3v = word(sumladiv3vs,i)
	ir = word(irs,i)
	#
	set yr [0:*]
	set ytics auto
	unset mytics
	set ylabel "$\\youngModulus_\\avg/\\linkMaterialStiffnessNormal$"
	set output foutput(sprintf("preprocessed_macromicro_E_I%s",ir))
	set key left top
	plot \
		sumladiv3v*(2+3*x)/(4+x) w l lt 4 lw 10 t "eq. (\\ref{eqMacroPropertiesElasticTheorElastConstantsApprox})" , \
		fresult(sprintf("preprocessed_%s_I%s_theor",bby,ir)) every ::0::nn u 1:($2/young) w p lt 1 pt pt1 ps pps t "eq. (\\ref{eqMacroPropertiesElasticTheorStiffnessTensor})" , \
		fresult(sprintf("preprocessed_%s_I%s",bby,ir)) every ::0::nn u 1:($2/young) w p lt 1 pt pt2 ps pps t "YADE" , \
		fresult(sprintf("preprocessed_%s_I%s",bbo,ir)) every ::0::nn u 1:($2/young) w p lt 1 pt pt3 ps pps t "OOFEM"
	#
	set yr [-1:.5]
	set ytics .5
	set mytics 5
	set grid mytics
	set key right top
	set ylabel "$\\poissonRatio_\\avg$"
	set output foutput(sprintf("preprocessed_macromicro_nu_I%s",ir))
	plot \
		(1-x)/(4+x) w l lt 4 lw 10 t "eq. (\\ref{eqMacroPropertiesElasticTheorElastConstantsApprox})" , \
		fresult(sprintf("preprocessed_%s_I%s_theor",bby,ir)) u 1:3 w p lt 1 pt pt1 ps pps t "eq. (\\ref{eqMacroPropertiesElasticTheorStiffnessTensor})" , \
		fresult(sprintf("preprocessed_%s_I%s",bby,ir)) u 1:3 w p lt 1 pt pt2 ps pps t "YADE" , \
		fresult(sprintf("preprocessed_%s_I%s",bbo,ir)) u 1:3 w p lt 1 pt pt3 ps pps t "OOFEM"
}


#======================================================================
reset
set term cairolatex pdf input size .5*W,H
load "settings.gpi"

set xr [-3.5:0]
set yr [-45:0]
set xlabel "strain [$10^{-3}$]"
set ylabel "stress [MPa]"
set xtics 1
set ytics 10
set key at -.3,-1.0

wo = 5
wy = 10
lsyp = 2
lsop = 3
lsyc = 4
lsoc = 1

b = "sequential_coupling"
set output foutput(b."_plain")
plot \
	fresult(b."_yade_plain")  u (mili($5)):(mega($6)) w l lw 5 t "CPM" , \
	fresult(b."_oofem_plain") u (mili($1)):(mega($2)) w l lw 5 t "DPM"

s0s = "-.43 -.86 -1.29 -1.71 -2.14 -2.57 -3.0 -.4 -0.85 -1.25 -1.98 -2.56"
do for [i=0:11] {
	bb = sprintf("_%03d",i)
	s0 = word(s0s,i+1)
	set output foutput(b.bb)
	plot \
		fresult(b."_yade_plain")  u    (mili($5)):(mega($6)) w l ls lsyp t "" , \
		fresult(b."_oofem_plain") u    (mili($1)):(mega($2)) w l ls lsop t "" , \
		fresult(b."_yade".bb)     u    (mili($5)):(mega($6)) w l ls lsyc lw wy t "CPM" , \
		fresult(b."_oofem".bb)    u (s0+mili($1)):(mega($2)) w l ls lsoc lw wo t "DPM"
}

set term cairolatex pdf input size .8*W,H
load "settings.gpi"
bb = "_dmg"
set yrange [-50:0]
set y2range [-.25:1]
set y2tics .25 nomirror
set y2label "damage [-]"
set border 11
set output foutput(b.bb)
set key at -1,-2
max1(a) = a > 1 ? 1 : a
dmgtmp(d1,d2,d3) = .85*d1+.15*d3
dmg(d1,d2,d3) = max1((2.09*dmgtmp(d1,d2,d3))**4.7)
plot \
	fresult(b."_yade_plain") u (mili($5)):(mega($6)) w l ls 1 t "CPM" axes x1y1, \
	fresult(b."_yade_plain") u (mili($5)):1 w l ls 2 t "$\\damageTensorI_1$" axes x1y2, \
	fresult(b."_yade_plain") u (mili($5)):2 w l ls 2 t "$\\damageTensorI_2$" axes x1y2, \
	fresult(b."_yade_plain") u (mili($5)):3 w l ls 3 t "$\\damageTensorI_3$" axes x1y2, \
	fresult(b."_yade_plain") u (mili($5)):(dmg($1,$2,$3)) w l ls 4 lw 10 t "eq. $(\\ref{eqCouplingSeqDmgTransformation})$" axes x1y2, \
	0 w l t ""


#======================================================================
reset
set term cairolatex pdf input size W,H
load "settings.gpi"
set yr [.8:1.5]
set xr [-.5:2.5]
set xtics ("9.5" 0, "12.5" 1, "19.0" 2) rotate by 90 right
b = "validationBeygi"
pb = "preprocessed_".b
set pointsize 1
do for [experiment in "l h"] {
	titles = "$\\youngModulus$ $f_t$ $f_c$ $G_f$"
	set output foutput(b."_".experiment)
	set multiplot layout 1,4 margins screen .10, .95, 0.30, .90 spacing screen 0.08, 0.1
	do for [i=1:4] {
		set title word(titles,i)
		set key Left reverse box opaque width 6 height 1 spacing 1.3 at screen .65, .9
		plot \
			faux(b."_experiment_".experiment)      u 5+i w lp lt 4 pt 1 lw 10 t "experiment" , \
			fresult(pb."_(.5,.5,.1)_".experiment)  u 5+i w lp lt 2 t "simulation 1" , \
			fresult(pb."_(.5,.5,.05)_".experiment) u 5+i w lp lt 3 t "simulation 2" , \
			NaN t ""
		unset key
	}
	unset multiplot
}
